#!/usr/bin/emacs --script

;;; ecukes.el --- Cucumber for Emacs

;; Copyright (C) 2010 Johan Andersson

;; Author: Johan Andersson <johan.rejeep@gmail.com>
;; Maintainer: Johan Andersson <johan.rejeep@gmail.com>
;; Version: 0.0.1
;; Keywords: test
;; URL: http://github.com/rejeep/ecukes

;; This file is NOT part of GNU Emacs.

;;; License:

;; This program is free software; you can redistribute it and/or modify
;; it under the terms of the GNU General Public License as published by
;; the Free Software Foundation; either version 3, or (at your option)
;; any later version.

;; This program is distributed in the hope that it will be useful,
;; but WITHOUT ANY WARRANTY; without even the implied warranty of
;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;; GNU General Public License for more details.

;; You should have received a copy of the GNU General Public License
;; along with GNU Emacs; see the file COPYING.  If not, write to the
;; Free Software Foundation, Inc., 51 Franklin Street, Fifth Floor,
;; Boston, MA 02110-1301, USA.

;;; Commentary:

;; See README

;;; Code:

(eval-when-compile
  (require 'cl))

;; Add the current directory to load-path
(add-to-list 'load-path (file-name-directory load-file-name))

(require 'ecukes-steps)
(require 'ecukes-init)
(require 'ecukes-def)
(require 'ecukes-output)
(require 'ecukes-parse)
(require 'ecukes-run)
(require 'ecukes-hooks)
(require 'ecukes-color)
(require 'ecukes-stats)
(require 'ecukes-misc)

(defvar ecukes-feature-files (ecukes-init-feature-files argv)
  "List containing all feature files.")


;; Load project
(ecukes-init-load-project argv)

;; Make sure there are feature files to run
(unless ecukes-feature-files
  (error "You did not provide any features"))

(dolist (feature-file ecukes-feature-files)
  (let* ((feature (ecukes-parse-feature feature-file))
         (background (ecukes-feature-background feature)))

    ;; Output intro
    (let ((intro (ecukes-feature-intro feature)))
      (if intro (ecukes-output-intro intro)))

    ;; Run and output background
    (if background
        (ecukes-output-background
         (ecukes-run-background
          background
          (lambda (step success)
            (ecukes-stats-update-steps success)
            (ecukes-output-step step success)))))

    (dolist (scenario (ecukes-feature-scenarios feature))
      ;; Run before hooks
      (ecukes-hooks-run-before)
      
      ;; Turn on/off debug.
      (let ((debug (member "debug" (ecukes-scenario-tags scenario))))
        (ecukes-message-advice (not debug)))
      
      ;; Run background
      (if background (ecukes-run-background background))

      ;; Run and output scenario
      (let ((success-scenario t))
        (ecukes-output-scenario
         scenario
         (ecukes-run-scenario
          scenario
          (lambda (step success-step)
            (unless success-step (setq success-scenario nil))
            (ecukes-stats-update-steps success-step)
            (ecukes-output-step step success-step))))
        (ecukes-stats-update-scenarios success-scenario))
      
      ;; Clear the list of messages
      (setq ecukes-messages '())

      ;; Run after hooks
      (ecukes-hooks-run-after)
      )))

(ecukes-stats-print-summary)

;;; ecukes.el ends here
